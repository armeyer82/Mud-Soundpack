#CLASS {Out-Of-Band} OPEN

#ACTION {^#$#mcp version:}
{
     #send #$#register_soundpack Tintin Soundpacks for Mac | $Version[1];
	.sp_keep_alive;
}

#ACTION {^#$#soundpack environment %w | %w | %w}
{
     #variable environment[structure] %1;
     #variable environment[safe] %2;
     #variable environment[lighted] %3
}

#ACTION {^#$#soundpack environment starship %w | %w | %w | %w}
{
     #variable environment[structure] starship;
     #variable environment[location] %1;
     #variable environment[powered] %2;
     #variable environment[safe] %3;
     #variable environment[lighted] %4
}

#ACTION {^#$#soundpack planet %1 | %w | %w}
{
     #variable environment[type] planet;
     #variable environment[safe] %2;
     #variable environment[lighted] %3
}

#ACTION {^#$#soundpack status %w}
{
     #switch {"%1"}
     {
          #case {"ok"}
          {
               #showme Your soundpack is up to date
          };
          #case {"unknown"}
          {
               #showme There was a problem registering your soundpack with the MOO.
          }
     }
}

#ACTION {^#$#soundpack_lag %*}
{
     #math {LAG[MS]} {%1 * 1000.00};
     #variable LAG[Date] @Date{};
     #variable LAG[Time] @Time{}
}

#ACTION {^#$#soundpack_pong}
{
     #send #$#soundpack_ping_reply m;
	#if {$Ping_Sound}
	{
		play General/Misc/Pong.wav
	}
}

#ACTION {^Your soundpack has been updated to version}
{
     play General/Misc/UpdateAlert.wav
}

#ALIAS {.sp_keep_alive}
{
	#if {$Keep_alive}
	{
		#ticker {Ping} {#send #$#soundpack_ping;#if {${Ping_Sound} == 1} {play General/Misc/Ping.wav}} {$Ping_Interval}
	}
}

#ALIAS {ping}
{
     #if {"%1"==""}
     {
          #showme As of $LAG[Date] at $LAG[Time], there is $LAG[MS] milliseconds of lag.
     };
     #else
     {
          #switch {"%1"}
          {
               #case {"on"}
               {
                    .sp_keep_alive;
                    #showme Enabling keep-alive pings.
               };
               #case {"off"}
               {
                    #unticker Ping;
                    #showme Disabling keep-alive pings.
               };
			#case {"mute"}
			{
				#show Muting ping sounds;
				#class User open;
				#var Ping_Sound 0;
				#class User close
			};
			#case {"unmute"}
			{
				#show Unmuting ping sounds;
				#class User open;
				#var Ping_Sound 1;
				#class User close
			};
               #case {"{(([0-9]{1,2})(:[0-9]{1,2})*)}"}
               {
                    #showme Setting ping interval to %1;
                    #variable Ping_Interval %1
               };
               #default
               {
                    #showme The command ping accepts either, "on", "off", or a time value.
               }
          }
     }
}

#VARIABLE         {Ping_Interval}  {60}
#VARIABLE         {Ping_Sound}  {1}
#var Keep_alive 1

#CLASS {Out-Of-Band} CLOSE
