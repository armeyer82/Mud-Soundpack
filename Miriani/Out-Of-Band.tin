#CLASS {Out-Of-Band} OPEN

#ACTION {^#$#mcp version:}
{
	#send #$#register_soundpack Tintin Soundpacks for Mac | $Version[1];
	#ticker {Ping} {#send #$#soundpack_ping;#nop play General/Misc/Ping.wav} {$Ping_Interval}
}
{5}

#ACTION {^#$#soundpack environment %w | %w | %w}
{
	#variable environment[structure] %1;
	#variable environment[safe] %2;
	#variable environment[lighted] %3
}
{5}

#ACTION {^#$#soundpack environment starship %w | %w | %w | %w}
{
	#variable environment[structure] starship;
	#variable environment[location] %1;
	#variable environment[powered] %2;
	#variable environment[safe] %3;
	#variable environment[lighted] %4
}
{5}

#ACTION {^#$#soundpack planet %1 | %w | %w}
{
	#variable environment[type] planet;
	#variable environment[safe] %2;
	#variable environment[lighted] %3
}
{5}

#ACTION {^#$#soundpack status %w}
{
	#switch {"%1"}
	{
		#case {"ok"}
		{
			#showme Your soundpack is up to date
		};
		#case {"unknown"}
		{
			#showme There was a problem registering your soundpack with the MOO.
		}
	}
}
{5}

#ACTION {^#$#soundpack_lag %*}
{
	#variable LAG[MS] %1;
	#variable LAG[Date] @Date{};
	#variable LAG[Time] @Time{}
}
{5}

#ACTION {^#$#soundpack_pong}
{
	#send #$#soundpack_ping_reply m;play General/Misc/Pong.wav
}
{5}

#ACTION {^Your soundpack has been updated to version}
{
	play General/Misc/UpdateAlert.wav
}
{5}

#ALIAS {ping}
{
	#showme As of $LAG[Date] at $LAG[Time], there is $LAG[MS] milliseconds of lag.
}
{5}

#gag  {^#$#}
#variable Ping_Interval 60

#CLASS {Out-Of-Band} CLOSE
