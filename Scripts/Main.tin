#CLASS {Main} OPEN

#ALIAS {LoadUserScripts}
{
	#read {$Userdir/User.tin}
}
{5}

#ALIAS {play %*.{wav|mp3|aiff}}
{
	#system play -qV0 -G $soundpath/%1.%2 gain -n -h vol $Volume[GameSounds] &
}
{5}

#ALIAS {ProgramExists}
{
	#script {if hash %1 2>/dev/null; then echo "#show %1 is installed."; else echo "#end %1 does not appear to be installed, but is required for the soundpack to function properly. Please install %1 and run the soundpack again. Exiting."; fi}
}

#EVENT {PROGRAM START}
{
	#script {Version} {cat ./Scripts/Version.txt};
	#show Welcome to the Mud Soundpack, Version $Version[1].;
	#show Written by A. R. Meyer;
	#show Running on %0 version %1.\n;
	#show Verifying necessary programs are installed.;
	ProgramExists sox;
	#read ./Scripts/Development.tin;
	#system mkdir -p ~/Mud-Files/
}

#EVENT {SESSION CONNECTED}
{
	#system mkdir -p ./$SESSION;
	#script {for i in $( ls ./%0/*.tin ); do echo "#read" $i; done};
	#variable SESSION %0;
	#system mkdir -p ~/Mud-Files/$SESSION/;
	#system mkdir -p ~/Mud-Files/$SESSION/Logs/;
	#variable Userdir @Homedir{}/Mud-Files/$SESSION/User-Scripts/;
	#variable Logdir @Homedir{}/Mud-Files/$SESSION/Logs/;
	#script {if [ -f $Userdir/User.tin ]; then echo "#read $Userdir/User.tin"; fi};
	#script {if [ -f $Userdir/UserSettings.tin ]; then echo "#read $Userdir/UserSettings.tin"; fi}
}

#FUNCTION {Homedir}
{
	#script {Homedir} {echo $HOME};
	#return $Homedir[1]
}

#FUNCTION {rnd}
{
	#math {result} {1d%1}
}


#CLASS {Main} CLOSE
